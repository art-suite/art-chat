import &ArtSuite

class Lobby extends FluxComponent
  @subscriptions
    chatsByChatRoom: :artSuite

  @stateFields
    currentUser:  null
    showLobby:    true
    chatRoom:     :artSuite
    ready:        false

  @getter
    canEnter: ->
      !!present @currentUser

  @setter
    currentUser: (u) ->
      @setState currentUser:
        if present u
          u.trim()
        else
          null

  enter: ->
    if @canEnter
      @showLobby = false
      @models.chat.create data: {}
        user:     @currentUser
        message:  "/entered the room"
        @chatRoom

  exit: ->
    unless @showLobby
      @showLobby = true
      @models.chat.create data: {}
        user:     @currentUser
        message:  "/left"
        @chatRoom

  renderLobbyUi: ->
    {gridSize} = &StyleProps
    Element
      key: :lobby
      axis: .5
      location: ps: .5
      size: w: 320, hch: 1
      childrenLayout: :column
      cacheDraw: true
      animators:
        location: duration: .5 toFrom: ps: .5, y: -70
        opacity:  duration: .5 toFrom: 0
        scale:    duration: .5 toFrom: .975
      Element
        size: hch: 1
        TextElement
          inFlow: false
          axis:   :bottomRight
          location: ps: 1
          text:   :Chat
          color:  &Palette.black.quandary
          fontSize: 92
        TextElement
          size:   cs: 1
          text:   :ArtSuite
          color:  :white
          fontSize: 48

      Element
        size:         hch: 1
        draw:
          rectangle: radius: bl: 5 br: 5
          color: :white

        TextInputElement
          &StyleProps.mediumText
          align:        :center
          padding:      10
          size:         hch: 1
          key:          :usernameInput
          placeholder:  "" select username
          value:        @currentUser
          maxLength:    32
          on: {}
            @enter
            valueChanged: ({target:{value}}) -> @setCurrentUser value

      Element size: h: 10

      &Button
        color: :white
        size: h: gridSize * 2
        text: "" join chat
        disabled: !@canEnter
        action: @enter

  render: ->
    Element
      if @showLobby
        if @ready
          @renderLobbyUi()
        else
          @ready = true
          null
      else
        Element
          key: :chatView
          animators: axis: duration: .5 toFrom: y: -1, x: .5
          axis: x: .5
          location: ww: .5
          size: ww: 1 max: x: 400
          &ChatView {} @currentUser, @chatRoom, @exit
